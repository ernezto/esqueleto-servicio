apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'flyway'
apply plugin: 'application'
apply plugin: 'gradle-one-jar'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'info.solidsoft.pitest'
apply from: 'configuracion.gradle'

compileJava.options.encoding = 'UTF-8'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-gradle-plugin:3.0'
        classpath 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.3.2'
        classpath 'com.github.rholder:gradle-one-jar:1.0.3'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.4'
    }
}

sourceSets {
    intTest {
        java {
            srcDir 'src/integration/java'
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources {
            srcDir 'src/integration/resources'
        }
    }
}

project.ext {
    dropwizardVersion = '0.7.0'
    archivoConfiguracion = 'servicio.yml'
    generatedSources = ['ec/gob/senescyt/**/dao/**/*']
}

repositories{
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/public"
    }
}

configurations {
    intTestCompile.extendsFrom(testCompile)
    intTestRuntime.extendsFrom(testRuntime)
}

dependencies {
    compile(
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-testing:' + dropwizardVersion,
            'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
            'io.dropwizard:dropwizard-metrics-graphite:' + dropwizardVersion,
            'org.slf4j:slf4j-api:1.7.5',
            'org.postgresql:postgresql:9.3-1101-jdbc41',
            'org.flywaydb:flyway-core:3.0',
            'commons-validator:commons-validator:1.4.0',
            'ec.gob.senescyt:sniese-commons:1.1.21',
            'com.github.oxo42:stateless4j:2.4.2',
            'com.sun.jersey.contribs:jersey-multipart:1.18.1',
            'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.4.0',
            'commons-io:commons-io:2.4',
    )
    testCompile(
            'junit:junit:4.11',
            'org.hamcrest:hamcrest-all:1.3',
            'org.mockito:mockito-all:1.9.5',
            'org.unitils:unitils-core:3.4.2',
            'pl.pragmatists:JUnitParams:1.0.3',
            'org.apache.commons:commons-math3:3.0',
            'com.github.mifmif:generex:0.0.3',
            'ec.gob.senescyt:sniese-commons-test:1.0.11',
            'org.powermock:powermock-module-junit4:1.5.6',
            'org.powermock:powermock-api-mockito:1.5.6',
    )
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

mainClassName = 'ec.gob.senescyt.ServicioApplication'

run {
    dependsOn += configurarServicio
    args 'server', archivoConfiguracion
}

test {
    jvmArgs '-noverify'
    dependsOn configurarServicio, pmdMain, pmdTest, pmdIntTest, findbugsMain, findbugsTest, findbugsIntTest
}

flyway {
    user = flyway.user
    password = flyway.password
    url = flyway.url
    schemas = [flyway.schemas]
}

tasks.withType(org.flywaydb.gradle.task.AbstractFlywayTask) {
    dependsOn += configurarServicio
}

tasks.withType(Test) {
    dependsOn += configurarServicio

    testLogging {
        showStandardStreams = true
        displayGranularity = 2
        exceptionFormat = 'full'
        events 'started', 'passed'
    }
    outputs.upToDateWhen { false }
}

task intTest(type: Test) {
    description 'Runs the integration tests.'
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath
}

task oneJar(type: OneJar, dependsOn: clean) {
    mainClass = mainClassName
    additionalDir = file('src/main/resources')
}

pmd {
    targetJdk = '1.7'
    toolVersion = '4.3'
}

project.ext.pmdRuleSets = [
        'basic',
        'braces',
        'clone',
        'codesize',
        'logging-java',
        'migrating',
        'strictexception',
        'strings',
        'typeresolution',
        'unusedcode',
]

pmdMain {
    ruleSets = pmdRuleSets + ['coupling', 'imports']
    ruleSetFiles = files('config/pmd/pmd.xml')
}

pmdTest {
    ruleSets = pmdRuleSets
    ruleSetFiles = files('config/pmd/pmd-test.xml')
}

pmdIntTest {
    ruleSets = pmdRuleSets
    ruleSetFiles = files('config/pmd/pmd-test.xml')
}

findbugs {
    toolVersion = '3.0.0'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

jacocoTestReport {
    description 'Generates test coverage report with JaCoCo.'
    doFirst {
        classDirectories = fileTree(dir: "${buildDir}/classes/main/").exclude(generatedSources)
    }
}

pitest {
    targetClasses = ['ec.gob.senescyt.*']
    threads = 4
    outputFormats = ['XML', 'HTML']
}

idea {
    module {
        testSourceDirs += file('src/integration/java')
        sourceDirs += file('src/integration/resources')

        iml {
            withXml {
                def testResourceFolders = it.asNode().component.content.sourceFolder
                        .findAll { it.@url in ['file://$MODULE_DIR$/src/integration/resources', 'file://$MODULE_DIR$/src/test/resources'] }
                testResourceFolders.each { resourceFolder ->
                    def atts = resourceFolder.attributes()
                    atts.remove('isTestSource')
                    atts.put('type', 'java-test-resource')
                }
                def mainResource = it.asNode().component.content.sourceFolder
                          .find { it.@url == 'file://$MODULE_DIR$/src/main/resources' }
                          .attributes()
                mainResource.remove('isSource')
                mainResource.put('type', 'java-resource')
            }
        }
    }
}