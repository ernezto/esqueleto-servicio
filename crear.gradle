def nombreMicroservicio
def directorioDestino
def plantillas = ['**/*.yml.example', 'deploy.gradle']

if (project.hasProperty('nombre')) {
    nombreMicroservicio = "servicio-$project.nombre"
    directorioDestino = "../$nombreMicroservicio"
}

task copiarArchivos(type: Copy) {
    if (project.hasProperty('nombre') && project.hasProperty('puerto') && project.hasProperty('kpass')) {
        if (new File(directorioDestino).exists()) {
            throw new StopExecutionException("El directorio de destino ($directorioDestino) ya existe")
        }

        from('.') {
            def gitignore = new File('.gitignore')
            gitignore.eachLine { exclude it }
            exclude plantillas + ['crear.gradle', 'gitignore']
        }

        into directorioDestino
    } else {
        throw new StopExecutionException('Uso correcto: ./gradlew -b crear.gradle -Pnombre=<nombre microservicio>' +
                ' -Ppuerto=<puerto inicial> -Pkpass=<contraseña del keystore>')
    }
}

task copiarGitignore(type: Copy) {
    from 'gitignore'
    into directorioDestino
    rename { nombreArchivo -> '.gitignore' }
}

task copiarKeystore(type: Copy) {
    from 'sniese.keystore'
    into directorioDestino
}

task copiarConfiguracion(type: Copy) {
    from ('.') {
        include plantillas
        expand(nombreMicroservicio: nombreMicroservicio, keyStorePassword: project.kpass, httpInicial: project.puerto,
                httpsInicial: calcularHttpsInicial(project.puerto))
    }
    rename { nombreArchivo -> nombreArchivo.replace('.example', '') }
    into directorioDestino
}

task gitInicializar(type: Exec) {
    workingDir directorioDestino
    commandLine 'git', 'init'
}

task gitAgregar(type: Exec) {
    workingDir directorioDestino
    commandLine 'git', 'add', '.'
}

task gitAcometer(type: Exec) {
    workingDir directorioDestino
    commandLine 'git', 'commit', '-m"[#0] Crear estructura inicial para microservicio"'
}

task imprimirInfo() {
    doLast {
        def urlOrganizacion = 'https://github.com/Senescyt'
        println '\n==============================================================================='
        println "Microservicio creado en $directorioDestino"
        println 'Repositorio de git inicializado'
        println "\nPor favor, crea el repositorio $nombreMicroservicio en GitHub en la cuenta de la organización:"
        println "  $urlOrganizacion"
        println "  $urlOrganizacion/$nombreMicroservicio"
    }
}

def calcularHttpsInicial(puertoHttp) {
    puertoHttp = puertoHttp.toInteger()
    final HTTP_SEMILLA = 8080
    final HTTPS_SEMILLA = 8443
    def diferenciaSemillas = HTTPS_SEMILLA - HTTP_SEMILLA
    def decrementoEntreProtocolos = (puertoHttp - HTTP_SEMILLA) / 2
    return puertoHttp + (diferenciaSemillas - decrementoEntreProtocolos)
}

defaultTasks 'copiarArchivos', 'copiarGitignore', 'copiarKeystore', 'copiarConfiguracion', 'gitInicializar',
        'gitAgregar', 'gitAcometer', 'imprimirInfo'