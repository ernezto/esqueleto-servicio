def nombreMicroservicio
def directorioDestino
def plantillas = ['**/*.yml.example', 'deploy.gradle']

if (project.hasProperty('nombre')) {
    nombreMicroservicio = "servicio-$project.nombre"
    directorioDestino = "../$nombreMicroservicio"
}

task copiarArchivos(type: Copy) {
    if (project.hasProperty('nombre') && project.hasProperty('puerto') && project.hasProperty('kpass')) {
        if (new File(directorioDestino).exists()) {
            throw new StopExecutionException("El directorio de destino ($directorioDestino) ya existe")
        }

        from('.') {
            def gitignore = new File('.gitignore')
            gitignore.eachLine { exclude it }
            exclude plantillas + ['crear.gradle', 'gitignore']
        }

        into directorioDestino
    } else {
        throw new StopExecutionException('Uso correcto: ./gradlew -b crear.gradle -Pnombre=<nombre microservicio>' +
                ' -Ppuerto=<puerto inicial> -Pkpass=<contrase침a del keystore>')
    }
}

task copiarGitignore(type: Copy) {
    from 'gitignore'
    into directorioDestino
    rename { nombreArchivo -> '.gitignore' }
}

task copiarKeystore(type: Copy) {
    def archivoKeystore = 'sniese.keystore'
    if (new File(archivoKeystore).exists()) {
        from archivoKeystore
        into directorioDestino
    } else {
        throw new StopExecutionException("No se encontr칩 el archivo $archivoKeystore")
    }
}

task expandirConfig(type: Copy) {
    from ('.') {
        include plantillas
        expand(nombreMicroservicio: nombreMicroservicio, httpInicial: project.puerto, httpsInicial: calcularHttpsInicial(project.puerto))
    }
    into directorioDestino
}

task expandirSecretos(type: Copy) {
    from (directorioDestino) {
        include plantillas
        rename { nombreArchivo -> nombreArchivo.replace('.example', '') }
        expand(keyStorePassword: project.kpass)
    }
    into directorioDestino
}

task gitInicializar(type: Exec) {
    workingDir directorioDestino
    commandLine 'git', 'init'
}

task gitAgregar(type: Exec) {
    workingDir directorioDestino
    commandLine 'git', 'add', '.'
}

task gitAcometer(type: Exec) {
    workingDir directorioDestino
    commandLine 'git', 'commit', '-m"[#0] Crear estructura inicial para microservicio"'
}

task imprimirInfo() {
    doLast {
        def organizacion = 'Senescyt'
        println '\n==============================================================================='
        println "Microservicio creado en $directorioDestino"
        println 'Repositorio de git inicializado'
        println "\nPor favor, crea el repositorio $nombreMicroservicio en GitHub en la cuenta de la organizaci칩n:"
        println "  https://github.com/organizations/$organizacion/repositories/new"
        println "  https://github.com/$organizacion/$nombreMicroservicio"
        println "\nPor favor, crea el repositorio privado configuracion-$nombreMicroservicio en Bitbucket en la cuenta de la organizaci칩n:"
        println "  https://bitbucket.org/repo/create"
        println "  https://bitbucket.org/$organizacion/configuracion-$nombreMicroservicio"
    }
}

def calcularHttpsInicial(puertoHttp) {
    puertoHttp = puertoHttp.toInteger()
    final HTTP_SEMILLA = 8080
    final HTTPS_SEMILLA = 8443
    def diferenciaSemillas = HTTPS_SEMILLA - HTTP_SEMILLA
    def decrementoEntreProtocolos = (puertoHttp - HTTP_SEMILLA) / 2
    return puertoHttp + (diferenciaSemillas - decrementoEntreProtocolos)
}

defaultTasks 'copiarArchivos', 'copiarGitignore', 'copiarKeystore', 'expandirConfig', 'expandirSecretos',
             'gitInicializar', 'gitAgregar', 'gitAcometer', 'imprimirInfo'
